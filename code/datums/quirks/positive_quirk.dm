//This file contains quirks that provide a gameplay advantage. Players are limited to a maximum of two of these quirks

/datum/quirk/apathetic
	name = "Apathetic"
	desc = "You are used to the awful things that happen here, bad events affect your mood less."
	icon = "meh"
	quirk_value = 1
	quirk_flags = QUIRK_HUMAN_ONLY|QUIRK_MOODLET_BASED
	medical_record_text = "Patient was administered the Apathy Evaluation Scale but did not bother to complete it."
	mail_goodies = list(/obj/item/hourglass)

/datum/quirk/drunkhealing
	name = "Drunken Resilience"
	desc = "Nothing like a good drink to make you feel on top of the world. Whenever you're drunk, you slowly recover from injuries."
	icon = "wine-bottle"
	quirk_value = 1
	gain_text = span_notice("You feel like a drink would do you good.")
	lose_text = span_danger("You no longer feel like drinking would ease your pain.")
	medical_record_text = "Patient has unusually efficient liver metabolism and can slowly regenerate wounds by drinking alcoholic beverages."
	quirk_flags = QUIRK_HUMAN_ONLY|QUIRK_PROCESSES
	mail_goodies = list(/obj/effect/spawner/random/food_or_drink/booze)

/datum/quirk/drunkhealing/process(delta_time)
	var/need_mob_update = FALSE
	switch(quirk_target.get_drunk_amount())
		if (6 to 40)
			need_mob_update += quirk_target.adjustBruteLoss(-0.1 * delta_time, updating_health = FALSE, required_bodytype = BODYTYPE_ORGANIC)
			need_mob_update += quirk_target.adjustFireLoss(-0.05 * delta_time, updating_health = FALSE, required_bodytype = BODYTYPE_ORGANIC)
		if (41 to 60)
			need_mob_update += quirk_target.adjustBruteLoss(-0.4 * delta_time, updating_health = FALSE, required_bodytype = BODYTYPE_ORGANIC)
			need_mob_update += quirk_target.adjustFireLoss(-0.2 * delta_time, updating_health = FALSE, required_bodytype = BODYTYPE_ORGANIC)
		if (61 to INFINITY)
			need_mob_update += quirk_target.adjustBruteLoss(-0.8 * delta_time, updating_health = FALSE, required_bodytype = BODYTYPE_ORGANIC)
			need_mob_update += quirk_target.adjustFireLoss(-0.4 * delta_time, updating_health = FALSE, required_bodytype = BODYTYPE_ORGANIC)
	if(need_mob_update)
		quirk_target.updatehealth()

/datum/quirk/empath
	name = "Empath"
	desc = "Whether it's a sixth sense or careful study of body language, it only takes you a quick glance at someone to understand how they feel."
	icon = "smile-beam"
	quirk_value = 1
	mob_trait = TRAIT_EMPATH
	gain_text = span_notice("You feel in tune with those around you.")
	lose_text = span_danger("You feel isolated from others.")
	medical_record_text = "Patient is highly perceptive of and sensitive to social cues, or may possibly have ESP. Further testing needed."

/datum/quirk/freerunning
	name = "Freerunning"
	desc = "You're great at quick moves! You can climb tables more quickly."
	icon = "running"
	quirk_value = 1
	mob_trait = TRAIT_FREERUNNING
	gain_text = span_notice("You feel lithe on your feet!")
	lose_text = span_danger("You feel clumsy again.")
	medical_record_text = "Patient scored highly on cardio tests."
	mail_goodies = list(/obj/item/melee/skateboard)

/datum/quirk/friendly
	name = "Friendly"
	desc = "You give the best hugs, especially when you're in the right mood."
	icon = "hands-helping"
	quirk_value = 1
	mob_trait = TRAIT_FRIENDLY
	gain_text = span_notice("You want to hug someone.")
	lose_text = span_danger("You no longer feel compelled to hug others.")
	quirk_flags = QUIRK_HUMAN_ONLY|QUIRK_MOODLET_BASED
	medical_record_text = "Patient demonstrates low-inhibitions for physical contact and well-developed arms. Requesting another doctor take over this case."
	mail_goodies = list(/obj/item/storage/box/hug)

/datum/quirk/jolly
	name = "Jolly"
	desc = "You sometimes just feel happy, for no reason at all."
	icon = "grin"
	quirk_value = 1
	quirk_flags = QUIRK_HUMAN_ONLY|QUIRK_MOODLET_BASED
	medical_record_text = "Patient demonstrates constant euthymia irregular for environment. It's a bit much, to be honest."
	mail_goodies = list(/obj/item/clothing/mask/joy)

/datum/quirk/jolly/process(seconds_per_tick)
	// 0.416% is 15 successes / 3600 seconds. Calculated with 2 minute
	// mood runtime, so 50% average uptime across the hour.
	if(DT_PROB(0.416, seconds_per_tick))
		SEND_SIGNAL(quirk_target, COMSIG_ADD_MOOD_EVENT, "jolly", /datum/mood_event/jolly)

/datum/quirk/jolly/remove()
	SEND_SIGNAL(quirk_target, COMSIG_CLEAR_MOOD_EVENT, "jolly")

/datum/quirk/light_step
	name = "Light Step"
	desc = "You walk with a gentle step; stepping on sharp objects is quieter, less painful and you won't leave footprints behind you. Also, your hands and clothes will not get messed in case of stepping in blood."
	icon = "shoe-prints"
	quirk_value = 1
	mob_trait = TRAIT_LIGHT_STEP
	gain_text = span_notice("You walk with a little more litheness.")
	lose_text = span_danger("You start tromping around like a barbarian.")
	medical_record_text = "Patient's dexterity belies a strong capacity for stealth."
	mail_goodies = list(/obj/item/clothing/shoes/sandal)

/datum/quirk/linguist
	name = "Linguist"
	desc = "Although you don't know every language, your intense interest in languages allows you to recognise the features of most languages."
	icon = "language"
	quirk_value = 1
	mob_trait = TRAIT_LINGUIST
	gain_text = span_notice("You can recognise the linguistic features of every language.")
	lose_text = span_danger("You can no longer recognise linguistic features for each language.")
	medical_record_text = "Patient possesses extrasensory language feature perception."

/datum/quirk/multilingual
	name = "Multilingual"
	desc = "You spent a portion of your life learning to understand an additional language. You may or may not be able to speak it based on your anatomy."
	icon = "comments"
	quirk_value = 1
	mob_trait = TRAIT_MULTILINGUAL
	gain_text = span_notice("You have learned to understand an additional language.")
	lose_text = span_danger("You have forgotten how to understand a language.")
	medical_record_text = "Patient knows more than one language."
	var/datum/language/known_language

/datum/quirk/multilingual/add(client/client_source)
	known_language = read_choice_preference(/datum/preference/choiced/quirk/multilingual_language)
	known_language ||= pick(GLOB.multilingual_language_list)
	quirk_target.grant_language(known_language, source = LANGUAGE_MULTILINGUAL)

/datum/quirk/multilingual/remove()
	if(!known_language)
		return
	quirk_target.remove_language(known_language, source = LANGUAGE_MULTILINGUAL)

/datum/quirk/night_vision
	name = "Night Vision"
	desc = "You can see slightly more clearly in full darkness than most people."
	icon = "eye"
	quirk_value = 1
	mob_trait = TRAIT_NIGHT_VISION_WEAK
	gain_text = span_notice("The shadows seem a little less dark.")
	lose_text = span_danger("Everything seems a little darker.")
	medical_record_text = "Patient's eyes show above-average acclimation to darkness."
	mail_goodies = list(
		/obj/item/flashlight/flashdark,
		/obj/item/food/grown/mushroom/glowshroom/shadowshroom,
	)

/datum/quirk/night_vision/add(client/client_source)
	refresh_quirk_holder_eyes()

/datum/quirk/night_vision/remove()
	refresh_quirk_holder_eyes()

/datum/quirk/night_vision/proc/refresh_quirk_holder_eyes()
	var/mob/living/carbon/human/human_quirk_holder = quirk_target
	var/obj/item/organ/eyes/eyes = human_quirk_holder.get_organ_by_type(/obj/item/organ/eyes)
	if(!eyes || eyes.lighting_alpha)
		return
	// We've either added or removed TRAIT_NIGHT_VISION_WEAK before calling this proc. Just refresh the eyes.
	eyes.Insert(human_quirk_holder, special = TRUE)

/datum/quirk/item_quirk/photographer
	name = "Psychic Photographer"
	desc = "You have a special camera that can capture a photo of ghosts. Your experience in photography shortens the delay between each shot."
	icon = "camera"
	quirk_value = 1
	mob_trait = TRAIT_PHOTOGRAPHER
	gain_text = span_notice("You know everything about photography.")
	lose_text = span_danger("You forget how photo cameras work.")
	medical_record_text = "Patient mentions photography as a stress-relieving hobby."
	mail_goodies = list(/obj/item/camera_film)

/datum/quirk/item_quirk/photographer/add_unique(client/client_source)
	give_item_to_holder(
		/obj/item/camera/spooky,
		list(
			LOCATION_NECK = ITEM_SLOT_NECK,
			LOCATION_LPOCKET = ITEM_SLOT_LPOCKET,
			LOCATION_RPOCKET = ITEM_SLOT_RPOCKET,
			LOCATION_BACKPACK = ITEM_SLOT_BACKPACK,
			LOCATION_HANDS = ITEM_SLOT_HANDS
		)
	)

/datum/quirk/selfaware
	name = "Self-Aware"
	desc = "You know your body well, and can accurately assess the extent of your wounds."
	icon = "bone"
	quirk_value = 1
	mob_trait = TRAIT_SELF_AWARE
	medical_record_text = "Patient demonstrates an uncanny knack for self-diagnosis."
	mail_goodies = list(/obj/item/clothing/neck/stethoscope)

/datum/quirk/skittish
	name = "Skittish"
	desc = "You can conceal yourself in danger. Ctrl-shift-click a closed locker to jump into it, as long as you have access."
	icon = "trash"
	quirk_value = 1
	mob_trait = TRAIT_SKITTISH
	medical_record_text = "Patient demonstrates a high aversion to danger and has described hiding in containers out of fear."
	mail_goodies = list(/obj/structure/closet/cardboard)

/datum/quirk/item_quirk/tagger
	name = "Tagger"
	desc = "You're an experienced artist. While drawing graffiti, you can get twice as many uses out of drawing supplies."
	icon = "spray-can"
	quirk_value = 1
	mob_trait = TRAIT_TAGGER
	gain_text = span_notice("You know how to tag walls efficiently.")
	lose_text = span_danger("You forget how to tag walls properly.")
	medical_record_text = "Patient was recently seen for possible paint huffing incident."
	mail_goodies = list(
		/obj/item/toy/crayon/spraycan,
		/obj/item/canvas/nineteen_nineteen,
		/obj/item/canvas/twentythree_nineteen,
		/obj/item/canvas/twentythree_twentythree
	)

/datum/quirk/item_quirk/tagger/add_unique(client/client_source)
	give_item_to_holder(/obj/item/toy/crayon/spraycan, list(LOCATION_BACKPACK = ITEM_SLOT_BACKPACK, LOCATION_HANDS = ITEM_SLOT_HANDS))

/datum/quirk/voracious
	name = "Voracious"
	desc = "Nothing gets between you and your food. You eat faster and can binge on junk food! Being fat suits you just fine."
	icon = "drumstick-bite"
	quirk_value = 1
	mob_trait = TRAIT_VORACIOUS
	gain_text = span_notice("You feel HONGRY.")
	lose_text = span_danger("You no longer feel HONGRY.")
	medical_record_text = "Patient has an above average appreciation for food and drink."
	mail_goodies = list(/obj/effect/spawner/random/food_or_drink/dinner)

/datum/quirk/neet
	name = "NEET"
	desc = "For some reason you qualified for social welfare."
	icon = "money-check-alt"
	quirk_value = 1
	gain_text = span_notice("You feel useless to society.")
	lose_text = span_danger("You no longer feel useless to society.")
	quirk_flags = QUIRK_HUMAN_ONLY|QUIRK_MOODLET_BASED
	medical_record_text = "Patient qualifies for social welfare."

/datum/quirk/neet/add_unique(client/client_source)
	var/mob/living/carbon/human/H = quirk_target
	var/datum/bank_account/D = H.get_bank_account()
	if(!D) //if their current mob doesn't have a bank account, likely due to them being a special role (ie nuke op)
		return
	D.payment_per_department[ACCOUNT_NEET_ID] += PAYCHECK_WELFARE

/datum/quirk/item_quirk/proskater
	name = "Skater Bro"
	desc = "You're a little too into old-earth skater culture! You're much more used to riding and falling off skateboards, needing less stamina to do kickflips and taking less damage upon bumping into something."
	icon = "hand-middle-finger"
	quirk_value = 1
	mob_trait = TRAIT_PROSKATER
	gain_text = span_notice("You feel like hitting a sick grind!")
	lose_text = span_danger("You no longer feel like you're in touch with the youth.")
	medical_record_text = "Patient demonstrated a high affinity for skateboards."

/datum/quirk/item_quirk/proskater/add_unique(client/client_source)
	give_item_to_holder(/obj/item/melee/skateboard/pro, list(LOCATION_BACKPACK = ITEM_SLOT_BACKPACK, LOCATION_HANDS = ITEM_SLOT_HANDS))

/datum/quirk/computer_whiz
	name = "Computer Whiz"
	desc = "You have always had a knack for technologies. You are able to manipulate and alter modular computer parts faster and safely."
	icon = "microchip"
	quirk_value = 1
	mob_trait = TRAIT_COMPUTER_WHIZ
	gain_text = span_notice("You feel much more confortable around technology.")
	lose_text = span_danger("You feel your love for technology dissipate.")
	medical_record_text = "Patient's vocational assessment test shows an affinity for technology."
